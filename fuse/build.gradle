import com.android.build.api.dsl.ManagedVirtualDevice

/*
Copyright 2023 Breautek 

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/

plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'com.breautek.fuse'

    compileSdk 34
    buildToolsVersion = '34.0.0'

    buildFeatures {
        buildConfig true
    }

    defaultConfig {
        minSdk 24

        // Required for instrumented tests
        targetSdk 34

        aarMetadata {
            minCompileSdk 24
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        buildConfigField("String", "FUSE_VERSION", "\"" + file('../VERSION').text.trim() + "\"")
    }

    buildTypes {
        debug {
            minifyEnabled false
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    testOptions {
        managedDevices {
            devices {
                api27 (ManagedVirtualDevice) {
                    device = "Pixel 7"
                    apiLevel = 27
                    systemImageSource = "aosp"
                }

                api28 (ManagedVirtualDevice) {
                    device = "Pixel 7"
                    apiLevel = 28
                    systemImageSource = "aosp"
                }

                api29 (ManagedVirtualDevice) {
                    device = "Pixel 7"
                    apiLevel = 29
                    systemImageSource = "aosp"
                }

                api30 (ManagedVirtualDevice) {
                    device = "Pixel 7"
                    apiLevel = 30
                    systemImageSource = "aosp"
                }

                api31 (ManagedVirtualDevice) {
                    device = "Pixel 7"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }

                api32 (ManagedVirtualDevice) {
                    device = "Pixel 7"
                    apiLevel = 32
                    systemImageSource = "aosp"
                }

                api33 (ManagedVirtualDevice) {
                    device = "Pixel 7"
                    apiLevel = 33
                    systemImageSource = "aosp"
                }

                api34 (ManagedVirtualDevice) {
                    device = "Pixel 7"
                    apiLevel = 34
                    systemImageSource = "google"
                }
            }
        }
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.webkit:webkit:1.8.0'
    implementation 'org.bouncycastle:bcprov-jdk18on:1.76'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.76'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test:rules:1.5.0'

    androidTestImplementation project(':EchoPlugin')
    androidTestImplementation project(':fuseTestTools')
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.breautek.fuse'
            artifactId = 'core'
            version = file('../VERSION').text.trim()

            afterEvaluate {
                from components.release
            }
        }
    }

    repositories {
        maven {
            url = 'https://archiva.breautek.com/repository/breautek'
            credentials {
                username findProperty('breautek.repository.user')
                password findProperty('breautek.repository.password')
            }
        }
    }
}
